%option always-interactive

%{

#include "sse-tools.h"

struct {
  char* id;
  char* event;
  char* data;
} buf = {0,0,0};

struct {
  const char* id;
  const char* event;
  const char* data;
} msg = {0,0,0};

/*
 * skip the first character in string, if there is one;
 * then replace the string in \a *pTarget with this string;
 * return a pointer to the string.
 */ 
static void do_set(char** pBuf, const char* string)
{
    *pBuf = realloc(*pBuf, strlen(string) + 1);
    strcpy(*pBuf, string);
}

static void do_add(char** pBuf, const char* string)
{
  if(!*pBuf) {
    do_set(pBuf, string);
  } 
  else {
    *pBuf = realloc(*pBuf, strlen(*pBuf) + strlen(string) + 1);
    strcat(*pBuf, "\n");
    strcat(*pBuf, string);
  }
}

#define set(name, ptr) do { do_set(&buf.name, ptr); msg.name = buf.name; } while(0)

#define add(name, ptr) do { do_add(&buf.name, ptr); msg.name = buf.name; } while(0)

static void flush()
{
    if(!msg.data) return;
    
    if(!msg.event)
      msg.event = "event";
    if(!msg.id)
      msg.id = "id";
    
    on_event(msg.event, msg.id, msg.data);

    msg.event = msg.id = msg.data = NULL;
    buf.event = buf.id = buf.data = NULL;
}

%}

%%

id:\ .*         set(id, yytext + 4);
id:.*           set(id, yytext + 3);
event:\ .*      set(event, yytext + 7);
event:.*        set(event, yytext + 6);
data:\ .*       add(data, yytext + 6);
data:.*         add(data, yytext + 5);

.*              { flush(); }
\n              { flush(); }

%%

#define HELP "sse-client connects to an URL, expects a server sent event stream, and runs a command on each event.\n\n" \
  "  sse-client URL command [ <option1> [ <option2> ... ] ]\n\n" \
  "The command to be called will receive event type and id as additional parameters; e.g. as\n\n" \
  "  command option1 option2 event id\n\n" \
  "and will receive data input on STDIN."

static void help() {
  fprintf(stderr, "sse-client, compiled %s %s.\n\n", __DATE__, __TIME__);
  fprintf(stderr, "%s\n", HELP);

  exit(1);
}

/*
 * curl callback to feed some data into the lexer.
 */
static size_t on_data(char *ptr, size_t size, size_t nmemb, void *userdata)
{
  YY_BUFFER_STATE buffer = yy_scan_bytes(ptr, size * nmemb);
  while(yylex());
  yy_delete_buffer(buffer);
  
  return size * nmemb;
}

int main(int argc, const char** argv) 
{
  (void)yyunput; // Suppress warning about unused symbol
  
  const char* url = NULL;
  
  argv++; argc--;
  
  /* == process url ================================================ */

  if(*argv) {
    url = *argv++;
    argc--;
  }

  /* == process command ============================================ */
  
  if(*argv) {
    /* Keep space for event, id, and data */
    command = calloc(argc + 4, sizeof(const char*));
    
    command_ofs = 0;
    while(*argv) {
      command[command_ofs++] = strdup(*argv++);
      argc--;
    }
  }

  /* == help needed? =============================================== */
  
  if(!command)
    help();

  connect_to_url(url, on_data);
  return 0;
}

int yywrap() { 
  return 1; 
}

